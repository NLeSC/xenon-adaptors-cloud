apply plugin: 'org.unbroken-dome.test-sets'

group = 'nl.esciencecenter.xenon.adaptors'

sourceSets {
    docGenerator
}

testSets {
    integrationTest
    fixedClientEnvironmentTest
    liveTest {
        options {
            // Pass all -D arguments from gradlew to tests
            systemProperties(System.getProperties())
        }
    }
}

integrationTest.description = 'Run the integration tests against docker containers'
fixedClientEnvironmentTest.description = 'Run the integration tests within and against docker containers, should be run within `xenonmiddleware/fixed-client` docker container'

check.dependsOn integrationTest
integrationTest.mustRunAfter test

dependencies {
    integrationTestImplementation group: 'junit', name: 'junit', version: '4.12'
    integrationTestImplementation group: 'com.palantir.docker.compose', name: 'docker-compose-rule-junit4', version: '0.33.0'

    integrationTestImplementation group: 'nl.esciencecenter.xenon', name: 'xenon-testapi', version: xenonVersion

//    fixedClientEnvironmentTestCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-junit4', version: '0.33.0'
//    fixedClientEnvironmentTestCompile group: 'junit', name: 'junit', version: '4.12'
//    fixedClientEnvironmentTestCompile group: 'nl.esciencecenter.xenon', name: 'xenon-testapi', version: xenonVersion

    liveTestImplementation group: 'nl.esciencecenter.xenon', name: 'xenon-livetest', version: xenonVersion

    docGeneratorImplementation group: 'nl.esciencecenter.xenon', name: 'xenon-docgenerator', version: '3.0.2'
    docGeneratorImplementation sourceSets.main.output
    docGeneratorRuntime configurations.runtime
}

jacocoTestCoverageVerification.group = 'Code coverage reporting'

jacoco {
    toolVersion = "0.8.3"
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    description 'Generate coverage report of integration tests'
    group 'Code coverage reporting'
    executionData integrationTest
    sourceSets sourceSets.main
    reports {
        xml.enabled = true // codecov depends on xml format report
    }
}

task jacocoFixedClientEnvironmentTestReport(type: JacocoReport) {
    description 'Generate coverage report of fixed client environment tests'
    group 'Code coverage reporting'
    executionData fixedClientEnvironmentTest
    sourceSets sourceSets.main
    reports {
        xml.enabled = true // codecov depends on xml format report
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'xenon-middleware_xenon-adaptors-cloud'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', "xenon-middleware"
        property 'sonar.language', 'java'
        property 'sonar.links.homepage', 'https://github.com/xenon-middleware/xenon-adaptors-cloud'
        properties["sonar.tests"] += sourceSets.integrationTest.allSource.srcDirs
        property 'sonar.jacoco.reportPaths', [jacocoTestReport.executionData, jacocoIntegrationTestReport.executionData]
    }
}
project.tasks["sonarqube"].dependsOn "check"

liveTest.description = 'Runs the integration tests against live systems'
// Never up to date, can not verify same config/props/remote is used second time
liveTest.outputs.upToDateWhen {
    false
}

task jacocoLiveTestReport(type: JacocoReport) {
    description 'Generate coverage report of live integration tests'
    group 'Code coverage reporting'
    executionData liveTest
    sourceSets sourceSets.main
    reports {
        xml.enabled = true // codecov depends on xml format report
    }
}

task adaptorDocumentation(type: JavaExec) {
    description 'Generates html snippet with options of adaptors'
    group 'documentation'
    main = 'nl.esciencecenter.xenon.utils.AdaptorDocGenerator'
    File output = file("${project.docsDir}/index.html")
    args = [output, "", "s3", "https://xenon-middleware.github.io/xenon/versions/${xenonVersion}/javadoc/"]
    classpath = sourceSets.docGenerator.runtimeClasspath
    outputs.file output
}

task publishSite(type: Copy) {
    description 'Publishes artifacts to site (aka /docs/ directory)'
    group 'Publishing'
    dependsOn 'adaptorDocumentation'
    from "${project.docsDir}/index.html"
    into "docs/"
}
